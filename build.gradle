plugins {
    id 'java'
    id 'idea'
    id 'net.ltgt.apt' version '0.8'
}

group 'gendb'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

ext {
    xjcSourcesPath = project.buildDir.absolutePath + "/generated/xjc"

    mapstructVersion = "1.2.0.Final"
    validationApiVersion = "2.0.0.Final"
}

dependencies {
    implementation "org.mapstruct:mapstruct-jdk8:$mapstructVersion"
    implementation "javax.validation:validation-api:$validationApiVersion"

    apt "org.mapstruct:mapstruct-processor:$mapstructVersion"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

sourceSets {
    main {
        java {
            srcDirs = srcDirs + ['build/generated/xjc']
        }
    }
}

task generateModel {
    doFirst {
        mkdir xjcSourcesPath
    }

    doLast {
        exec {
            commandLine "xjc", "-d", xjcSourcesPath, "-p", "com.gendb.dto", "src/main/resources/config-schema.xsd", "-readOnly", "-no-header"
        }
    }
}

tasks.compileJava.dependsOn(generateModel)
